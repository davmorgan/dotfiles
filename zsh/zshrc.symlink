# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Setup Initial Prompt Config
setopt prompt_subst
autoload -U colors && colors # Enable colors in prompt

##
# Load Up Proper Helpfiles
unalias run-help
autoload run-help
HELPDIR=/usr/local/share/zsh/helpfiles

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit

unsetopt menu_complete   # do not autoselect the first completion entry
unsetopt flowcontrol
unsetopt nomatch
setopt auto_menu         # show completion menu on succesive tab press
setopt complete_in_word
setopt always_to_end

zmodload -i zsh/complist

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Setup Python Environment
export PYTHONPATH=\"$(brew --prefix)/lib/python2.7/site-packages:\$PYTHONPATH\"

# Lets load up our rbenv environment
export RBENV_ROOT=/usr/local/var/rbenv
if which rbenv > /dev/null; then eval "$(rbenv init -)"; fi

##
# Setup JAVA_HOME
export JAVA_HOME=$(/usr/libexec/java_home)

##
# AsciiDoc Environment Setup
export XML_CATALOG_FILES=/usr/local/etc/xml/catalog
